
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

version = "2.1.0-beta5-alpha3"
group= "co.bugg.quickplay"
archivesBaseName = "Quickplay-1.8.9"

compileJava.options.encoding = "UTF-8"

targetCompatibility = sourceCompatibility = 1.8

idea {
    module {
        inheritOutputDirs = true
    }
}

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.java-websocket:Java-WebSocket:1.5.1"
}


jar {
    // Add the license to the jar
    from "LICENSE"
}
shadowJar {
    configurations = [ project.configurations.compile ]
    classifier = ''
    relocate 'org.java_websocket', 'co.bugg.quickplay.shade.org.java_websocket'
    relocate 'org.slf4j', 'co.bugg.quickplay.shade.org.slf4j'
}

jar.finalizedBy shadowJar

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Fix asset loading
task moveResources {
    doLast {
        ant.move file: "${buildDir}/resources/main",
                todir: "${buildDir}/classes/java"
    }
}
moveResources.dependsOn processResources
classes.dependsOn moveResources